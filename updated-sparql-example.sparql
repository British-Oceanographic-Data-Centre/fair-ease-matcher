PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ex: <http://www.example.org/resources#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX sdo: <https://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?MatchURI ?MatchProp ?MatchTerm ?SearchTerm ?Container ?ContainerLabel ?Weight ?ExactMatch
{
  VALUES ?SearchTerm {"salinity" "nitrite concentration parameters in the water column" "alkalinity acidity and ph of the water column" "particulate total and organic nitrogen concentrations in the water column"
  }
  (?MatchURI ?Weight ?MatchTerm ?graph ?MatchProp) text:query (skos:definition skos:prefLabel dcterms:description skos:altLabel dcterms:identifier ?SearchTerm 500) .

  OPTIONAL {
    ?MatchURI skos:prefLabel|rdfs:label|dcterms:title|sdo:name ?match_label .
  }
  OPTIONAL {
    ?MatchProp skos:prefLabel|rdfs:label|dcterms:title|sdo:name ?prop_label .
  }
  OPTIONAL {
    ?OptContainer skos:member ?MatchURI ;
                 skos:prefLabel ?OptContainerLabel .
  }
  BIND(COALESCE(?OptContainer,"") AS ?Container)
  BIND(COALESCE(?OptContainerLabel,"") AS ?ContainerLabel)
  BIND(LCASE(?SearchTerm) = LCASE(?MatchTerm) AS ?ExactMatch)
} ORDER BY DESC(?ExactMatch) DESC(?Weight)